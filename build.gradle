group 'com.cn.cly'
version '1.0-SNAPSHOT'

//当前根目录配置优先级高于子模块配置 如果此处配置与子模块配置有一致部分 则会覆盖子模块

buildscript { //构建脚本
    //定义版本号
    ext {
        springBootVersion = '3.2.0'
    }
    repositories {//jar包仓库
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {  //子项目通配
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    //编译使用UTF-8
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {  //所有子项目jar包中央仓库
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        mavenCentral()
    }

    //定义版本
    ext {
        jasyptVersion = '3.0.5'
        docVersion = '2.1.0'
        jsoupVersion = '1.14.3'
        nekohtmlVersion = '1.9.22'
        hutoolVersion = '5.8.21'
        guavaVersion = '32.1.2-jre'
        aliOssVersion = '3.16.3'
        druidVersion = '1.2.18'
        dialectVersion = '3.1.0'
        locatorVersion = '0.52'
        layuiVersion = '2.8.8'
        treegridVersion = '0.3.0'
        logstashVersion = '7.4'
        jacksonVersion = '2.13.3'
    }

    dependencies {  //所有子项目通用依赖
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-docker-compose'
        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:${jasyptVersion}"
        implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
